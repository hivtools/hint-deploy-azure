@description('The location where the resources will be created.')
param location string = resourceGroup().location

@description('Prefix for naomi resources')
param prefix string

@description('The name of the main hint resource group')
param hintResourceGroup string

@description('The name of the blob storage for function app.')
param faBlobStorageAccountName string

@description('string for connecting to redis')
param redisConnectionString string

resource blobStorageAccount 'Microsoft.Storage/storageAccounts@2025-06-01' existing = {
  name: faBlobStorageAccountName
}

param subnetId string

@description('The name of the log analytics workspace. If set, it overrides the name generated by the template.')
param logAnalyticsWorkspaceName string

@description('The resource group the log analytics workspace is in.')
param logsResourceGroup string

resource logAnalyticsWorkspace 'Microsoft.OperationalInsights/workspaces@2021-06-01' existing = {
  name: logAnalyticsWorkspaceName
  scope: resourceGroup(logsResourceGroup)
}

param activeJobsFunctionAppName string

param faStorageContainerName string

resource appServicePlan 'Microsoft.Web/serverfarms@2024-04-01' = {
  name: '${prefix}-fa-SP'
  location: location
  kind: 'functionapp'
  sku: {
    tier: 'FlexConsumption'
    name: 'FC1'
  }
  properties: {
    reserved: true
  }
}

resource applicationInsights 'Microsoft.Insights/components@2020-02-02' = {
  name: 'insights-${activeJobsFunctionAppName}'
  location: location
  kind: 'web'
  properties: {
    Application_Type: 'web'
    WorkspaceResourceId: logAnalyticsWorkspace.id
    DisableLocalAuth: true
  }
}

resource activeJobsFunctionApp 'Microsoft.Web/sites@2025-03-01' = {
  name: activeJobsFunctionAppName
  location: location
  kind: 'functionapp,linux'
  identity: {
    type: 'SystemAssigned'
  }
  properties:{
    serverFarmId: appServicePlan.id
    virtualNetworkSubnetId: subnetId
    httpsOnly: true
    functionAppConfig: {
      deployment: {
        storage: {
          type: 'blobContainer'
          value: '${blobStorageAccount.properties.primaryEndpoints.blob}${faStorageContainerName}'
          authentication: {
            type: 'SystemAssignedIdentity'
          }
        }
      }
      runtime: {
        name: 'node'
        version: '24'
      }
      scaleAndConcurrency: {
        instanceMemoryMB: 512
        maximumInstanceCount: 40
      }
    }
  }
  resource configAppSettings 'config' = {
    name: 'appsettings'
    properties: {
      AzureWebJobsStorage__accountName: blobStorageAccount.name
      REDIS_CONNECTION_STRING: redisConnectionString
      APPINSIGHTS_INSTRUMENTATIONKEY: applicationInsights.properties.InstrumentationKey
    }
  }
}

module roleAssignmentBlobOwner '../roles/role_assignment.bicep' = {
  name: 'blobOwnerRoleAssignment'
  scope: resourceGroup(hintResourceGroup)
  params: {
    roleDefinitionName: 'b7e6dc6d-f1e8-4753-8033-0f276bb0955b'
    targetResourceId: activeJobsFunctionApp.id
    targetResourcePrincipalId: activeJobsFunctionApp.identity.principalId
  }
}

module roleAssignmentStorageContributor '../roles/role_assignment.bicep' = {
  name: 'storageContribRoleAssignment'
  scope: resourceGroup(hintResourceGroup)
  params: {
    roleDefinitionName: '17d1049b-9a84-46fb-8f53-869881c3d3ab'
    targetResourceId: activeJobsFunctionApp.id
    targetResourcePrincipalId: activeJobsFunctionApp.identity.principalId
  }
}

module roleAssignmentStorageQueueContributor '../roles/role_assignment.bicep' = {
  name: 'storageQueueContribRoleAssignemnt'
  scope: resourceGroup(hintResourceGroup)
  params: {
    roleDefinitionName: '974c5e8b-45b9-4653-ba55-5f855dd0fb88'
    targetResourceId: activeJobsFunctionApp.id
    targetResourcePrincipalId: activeJobsFunctionApp.identity.principalId
  }
}

module roleAssignmentAppInsights '../roles/role_assignment.bicep' = {
  name: 'appInsightsRoleAssignment'
  scope: resourceGroup(hintResourceGroup)
  params: {
    roleDefinitionName: '3913510d-42f4-4e42-8a64-420c390055eb'
    targetResourceId: activeJobsFunctionApp.id
    targetResourcePrincipalId: activeJobsFunctionApp.identity.principalId
  }
}


output functionAppUrl string = 'https://${activeJobsFunctionApp.properties.defaultHostName}/api/getActiveJobs'
